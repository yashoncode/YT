import com.diffplug.spotless.LineEnding

buildscript {
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.4.10")
    }
}

// Top-level build file
plugins {
    id("com.android.application") version "9.3.1" apply false
    id("com.android.library") version "9.3.1" apply false
    id("org.jetbrains.kotlin.plugin.compose") version "2.4.10" apply false
    id("com.google.dagger.hilt.android") version "2.60.1" apply false
    id("org.jetbrains.kotlin.plugin.serialization") version "2.4.10" apply false
    id("com.google.devtools.ksp") version "2.3.12" apply false
    id("com.android.test") version "9.3.1" apply false
    id("androidx.baselineprofile") version "1.5.0-rc01" apply false
    id("com.diffplug.spotless") version "8.10.0"
    alias(libs.plugins.room) apply false
}

spotless {
    // Adopt formatting incrementally: only files touched since this commit are linted. Moved
    // forward from 52c4928e because the twenty-one files still failing there are legacy ones no
    // current work goes near, and leaving them in scope meant every build failed the format gate
    // before it reached a compiler. Each gets cleaned up the next time someone edits it.
    ratchetFrom("6da3f34cad30caed7030020d0f771790472c374d")
    lineEndings = LineEnding.UNIX

    val ktlintConfig =
        mapOf(
            "ij_kotlin_packages_to_use_import_on_demand" to "**",
        )

    kotlin {
        target(
            "app/src/**/*.kt",
            "benchmark/src/**/*.kt",
        )
        targetExclude(
            "**/build/**",
            "**/generated/**",
        )
        ktlint("1.8.0").editorConfigOverride(ktlintConfig)
    }

    kotlinGradle {
        target(
            "*.gradle.kts",
            "app/*.gradle.kts",
            "benchmark/*.gradle.kts",
        )
        targetExclude(
            "**/build/**",
        )
        ktlint("1.8.0").editorConfigOverride(ktlintConfig)
    }
}

tasks.register("ktlintCheck") {
    group = "verification"
    description = "Checks Kotlin formatting with ktlint."
    dependsOn("spotlessCheck")
}

tasks.register("ktlintFormat") {
    group = "formatting"
    description = "Formats changed Kotlin files with ktlint."
    dependsOn("spotlessApply")
}
